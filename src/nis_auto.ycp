/**
 * File:
 *   nis_auto.ycp
 *
 * Package:
 *   Configuration of NIS
 *
 * Summary:
 *   Client for autoinstallation
 *
 * Authors:
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param first a map of NIS settings
 * @return map edited settings or an empty map if canceled
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = CallModule ("nis_auto", [ mm ]);
 */

{
    textdomain "nis";

    import "Nis";
    import "Wizard";
    include "nis/ui.ycp";

    list args = Args ();
    if ( size (args) <= 0 )
    {
	y2error ("Did not get the settings, probably some mistake...");
	return false;
    }
    if ( !is ( Args (0), map ) )
    {
	y2error ("Bad argument for nis_auto: %1", Args (0));
	return false;
    }    
    map settings = $[];
    {
	integer i = 0;
	while (i < size (Args()))
	{
	    if (is (Args (i), map) && nil != Args (i))	settings = Args (i);	    
	    i = i + 1;
	}
    }    
    
    Nis::Import ( settings );
    y2milestone("Imported: (%1)", settings);
    
    define set_contents()
	``{	
	term contents =
	    `VBox(
		  `VSpacing(1),
		  `RichText( `id(`summary), Nis::Summary()),
		  `VSpacing(0.5),
		  `HBox(
			`PushButton(`id(`configure), _("&Configure NIS")),
			`HStretch(),
			`PushButton(`id(`reset), _("&Reset Configuration"))
			),
		  `VSpacing(1)
		  );
	Wizard::SetContents(_("NIS Configuration"),
			    contents, "", true, true);
    }


    Nis::_autofs_allowed = true;
    set_contents();
    any result = nil;
    any ret = nil;
    repeat {
	ret = UI::UserInput();
	if (ret == `configure)
	{	    
	        Wizard::CreateDialog ();
		Wizard::ReplaceAbortButton(`Empty ());
		result = NisDialog ();				
		UI::CloseDialog ();
		if (result == `next || result == `finish)
		{
		     settings = Nis::Export ();
		} 
		Nis::Set(settings);
		set_contents();
	}
	else if ( ret == `reset)
	{
	    settings= $[];
	    Nis::Set(settings);
	    set_contents();
	}		
    } until (ret == `back || ret == `next || ret ==`key);   
  
    return [ret, settings];
}



