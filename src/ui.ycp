/**
 * File:
 *   include/nis/ui.ycp
 *
 * Package:
 *   Configuration of NIS
 *
 * Summary:
 *   User interface functions.
 *
 * Authors:
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 *
 * All user interface functions.
 *
 */

{
    textdomain "nis";

    import "Wizard";
    import "Nis";

    include "require.ycp";
    include "wizard/sequencer.ycp";
    include "ui/common_popups.ycp";
    include "network/ip.ycp";
    include "network/dns.ycp";

    /**
     * A Wizard Sequencer helper
     * @return	`next
     */
    global define symbol JustNext () ``{
	return `next;
    }

    global map Dialogs = $[
	"switch":	``(SwitchDialog ()),
	"ismultiple":	[ ``(IsMultiple ()), true ],
	"single":	``(NisDialog ()),
	"multiple":	``(MultipleDialog ()),
	"expert":	``(ExpertDialog ()),
	"end":		``(SaveDialog ()),
	"common-next":	[ ``( JustNext () ), true ],
	];

    global map Sequence = $[
	"ws_start"  : "switch",
	"switch"    : $[
	    `next   : "ismultiple",
	    `finish : "common-next",
	    `abort  : `abort,
	    ],
	"ismultiple": $[
	    `no     : "single",
	    `yes    : "multiple",
	    ],
	"single"       : $[
	    `next   : "common-next",
	    `multiple: "multiple",
	    `expert : "expert",
	    `abort  : `abort
	    ],
	"multiple"  : $[
	    `next   : "common-next",
	    `single : "single",
	    `expert : "expert",
	    `abort  : `abort
	    ],
	"expert"       : $[
	    `next   : "ismultiple",
	    `abort  : `abort
	    ],
	// This is will make AutoSequence finish without
	// confirmation. NormalSequence overrides it.
	"common-next" : $[
	    `next	: `next,
	    ],
	"end"       : $[
	    `next    : `next
	    ]
	];

    /**
     * The normal workflow
     * @return `back, `abort or `next
     */
    global define symbol NormalSequence() ``{
	map normal_override = $[
	    "common-next": $[
		`next: "end",
		],
	    ];

	Wizard::CreateDialog ();
	Nis::Read ();
	// the second map must override the first!
	symbol result = WizardSequencer (Dialogs,
					 union (Sequence, normal_override));

	if (result == `next)
	{
	    // Install packages if needed.
	    // Cannot do it in Write, autoinstall does it differently.
	    if (size (Nis::install_packages) > 0 ||
		size (Nis::remove_packages) > 0)
	    {
		// require.ycp
		DoInstallAndRemove (Nis::install_packages,
				    Nis::remove_packages);
	    }

	    if (Nis::Write ())
	    {
		if (Nis::start)
		{
		    if (Nis::DomainChanged ())
		    {
			// waring popup
			UI::WarningPopup (_("The domain has changed.
You must reboot for the changes to take effect."));
		    }
		    else
		    {
			// message popup
			UI::MessagePopup(_("The configuration of the NIS client
was saved and can be used."));
		    }
		}
		else
		{
		    // message popup
		    UI::MessagePopup(_("The configuration of the NIS
client was saved."));
		}
	    }
	}
	UI::CloseDialog();
	return result;
    }

    /**
     * The autoinstallation workflow
     * @return `back, `abort or `next
     */
    global define symbol AutoSequence() ``{
	Wizard::CreateDialog ();
	symbol ret = WizardSequencer (Dialogs, Sequence);
	UI::CloseDialog ();
	return ret;
    }

    global /*const*/ string broadcast_help =
	// Translators: network broadcast address
	_("<p>The <b>Broadcast</b> option enables searching
in the local network to find a server after the specified servers
fail to respond. It is a security risk.</p>
");

    global /*const*/ string details_help =
	_("<p><b>Details</b> gives access to some
less frequently used settings.</p>
");

    /**
     * The switch dialog.
     * Selects NIS: off/on/on with dhcp, Automount off/on
     * @return	`back, `abort, `next, `finish
     */
    global define symbol SwitchDialog () ``{
	Wizard::SetScreenShotName ("nis-client-1-start");

	// help text 1/3
	string help_text = _("<p>Here, your machine can be set up as an
 <b>NIS client</b>.</p>
");

	// help text 3/3
	help_text = help_text + _("<p>The Network Information Service (NIS)
 was formerly known as Sun <b>Yellow Pages</b> (YP). The name Yellow
 Pages is a registered trademark in the United Kingdom by British
 Telecommunications plc and may not be used without
 permission.</p>");

	// help text 4/3
	string autofs_help_text = _("<p>Automounter is a daemon that mounts directories automatically.
It is assumed that its configuration files (auto.*) already exist,
either locally or over NIS.<br>
If it is not installed and you want to use it, it will be installed
automatically.</p>");

	boolean yp_client = Nis::start;
	boolean autofs = Nis::_start_autofs;

	term con = nil;
	term nis_frame =
	    // frame label
	    `Frame (_("NIS client"),
		    `VBox (`VSpacing (0.2),
			   `RadioButtonGroup (
			       `id (`rd),
			       `Left (
				   `HVSquash (
				       `VBox (
					   // radio button label
					   `Left (`RadioButton (`id (`nisno), `opt (`notify), _("Do &not use NIS"), !yp_client)),
					   // radio button label
					   `Left (`RadioButton (`id (`nisyes), `opt (`notify), _("&Use NIS"), yp_client))
					   )))),
			   `VSpacing (0.2)
			)
		);

	term autofs_frame =
	    `Frame (
		`opt (`hstretch),
		// frame label
		_("Automounter"),
		`VBox (
		    `VSpacing (0.2),
		    // check box label
		    `CheckBox (`id(`autofs), _("Start &Automounter"), autofs),
		    `VSpacing (0.2),
		    `Empty ()
		    )
		);

	if (Nis::_autofs_allowed)
	{
	    con = `HVSquash (`VBox (nis_frame, `VSpacing (1), autofs_frame));
	    help_text = help_text + autofs_help_text;
	}
	else
	{
	    con = `HVSquash (`VBox (nis_frame, `Empty (`id (`autofs))));
	}

	Wizard::SetContentsButtons (
	    // dialog title
	    _("Configuration of NIS client"), con, help_text,
	    BackButtonLabel (),
	    yp_client? NextButtonLabel (): FinishButtonLabel ());

	any result = nil;
	repeat
	{
	    result = UI::UserInput ();
	    if (result == `cancel)
	    {
		result = `abort;
	    }

	    yp_client = (UI::QueryWidget (`id (`rd), `CurrentButton) != `nisno);

	    if (result == `nisyes || result == `nisno || result == `nisdhcp)
	    {
		UI::ChangeWidget (
		    `id (`next), `Label,
		    yp_client? NextButtonLabel (): FinishButtonLabel ());
	    }
	}
	until (result == `back || result == `next ||
	       (result == `abort && UI::ReallyAbortPopup (Nis::touched)));

	if (result == `next)
	{
	    Nis::start = yp_client;
	    Nis::_start_autofs = Nis::_autofs_allowed && UI::QueryWidget (`id (`autofs), `Value);
	    result = yp_client? result: `finish;
	}

	Wizard::RestoreScreenShotName ();
	return result;
    }

    /**
     * Decide whether a complex dialog for multiple domains will be displayed
     * @return `yes or `no
     */
    global define symbol IsMultiple () ``{
	return size (Nis::multidomain_servers) > 0 ? `yes: `no;
    }

    /**
     * The simple dialog
     * @return	`back, `abort, `next, `multiple or `expert
     */
    global define symbol NisDialog () ``{
	Wizard::SetScreenShotName ("nis-client-2a-single");

	// help text 1/3
	string help_text = _("<p>Enter your NIS domain (e.g, foo.com)
 and the NIS server's address (e.g., nis.foo.com or 10.20.1.1).</p>");

	// help text 2/3
	help_text = help_text + _("<p>Specify multiple servers
by separating their addresses with spaces.</p>
");
	// help text 3/3
	help_text = help_text + broadcast_help;

	// help text 3/3
	help_text = help_text + details_help;

	// help text 2/3
	help_text = help_text + _("<p>If you need to access
<b>Multiple domains</b>, use the button to switch to a detailed dialog.</p>");


	string yp_domain  = Nis::GetDomain ();
	string yp_address = Nis::GetServers ();

	// In this simple case, let's discard the distinction.
	boolean default_broadcast =
	    Nis::default_broadcast || Nis::global_broadcast;

	term con = nil;
	term nis_frame =
	    // frame label
	    `Frame (_("NIS client"),
		    `VBox (`VSpacing (0.2),
			   // text entry label
			   `TextEntry (`id (`ypd), _("NIS &domain"), yp_domain),
			   `VSpacing (0.2),
			   // text entry label
			   `TextEntry (`id (`ypa), _("&Addresses of NIS servers"), yp_address),
			   // check box label
			   `Left (`CheckBox (`id (`broadcast), `opt (`notify), _("Br&oadcast"), default_broadcast)),
			   `VSpacing (0.2),
			   `HBox (
			       // button label
			       `PushButton (`id (`expert), _("D&etails ...")),
			       // button label
			       `PushButton (`id (`multiple), _("&Multiple domains >>"))
			       ),
			   `VSpacing (0.2)
			)
		);

	con = `HVSquash (nis_frame);

	Wizard::SetContentsButtons (
	    // dialog title
	    _("Configuration of NIS client"), con, help_text,
	    BackButtonLabel (), FinishButtonLabel ());

	any result = nil;
	repeat
	{
	    result = UI::UserInput ();
	    if (result == `cancel)
	    {
		result = `abort;
	    }

	    if (contains ([`next, `expert, `multiple], result))
	    {
		yp_domain =  UI::QueryWidget (`id (`ypd), `Value);
		yp_address = UI::QueryWidget (`id (`ypa), `Value);
		default_broadcast = UI::QueryWidget (`id (`broadcast), `Value);

		if (!Nis::check_nisdomainname (yp_domain))
		{
		    // error popup label
		    UI::SetFocus (`id (`ypd));
		    UI::ErrorPopup (Nis::valid_nisdomainname ());
		    result = nil;
		    continue;
		}

		list temp_ad = filter (string a, splitstring (yp_address, " "),
				       ``(a != ""));
		if (size (temp_ad) == 0 ||
		    find (string a, temp_ad,
			  ``(!Nis::check_address_nis (a))) != nil)
		{
		    // error popup label
		    UI::MessagePopup (Nis::valid_address_nis ());
		    result = nil;
		    continue;
		}
	    }
	}
	until (result == `back || result == `next || result == `expert ||
	       result == `multiple ||
	       (result == `abort && UI::ReallyAbortPopup (Nis::touched)));

	if (contains ([`next, `expert, `multiple], result))
	{
	    Nis::SetDomain (yp_domain);
	    Nis::SetServers (yp_address);
	    Nis::Touch (Nis::default_broadcast != default_broadcast);
	    Nis::default_broadcast = default_broadcast;
	    Nis::Touch (Nis::global_broadcast != false);
	    Nis::global_broadcast = false;
	}

	Wizard::RestoreScreenShotName ();
	return result;
    }


    /**
     * The expert dialog
     * @return	`back, `abort or `next
     */
    global define symbol ExpertDialog () ``{
	Wizard::SetScreenShotName ("nis-client-2c-expert");

	// help text 1/3
	string help_text = _("<p>Normally, it is possible for any host to query which server a client is using. <b>Answer to the local host only</b> prevents it.</p>");

	// help text 2/3
	// Check, ie. turn on a check box
	help_text = help_text + _("<p>Check <b>Broken server</b> if answers from servers running on an unprivileged port should be accepted. It is a security risk and it is better to replace such a server.</p>");

	// help text 3/3
	help_text = help_text + _("<p>See <b>man ypbind</b> for details on other options.</p>");

	boolean local_only = Nis::local_only;
	boolean broken_server = Nis::broken_server;
	string options = Nis::options;

	term contents =
	    `HVSquash (
		`Frame (
		    _("Expert settings"),
		    `VBox (
			`VSpacing (0.2),
			// check box label
			`Left (`CheckBox (`id (`local_only), _("Answer to the &local host only"), local_only)),
			// check box label
			`Left (`CheckBox (`id (`broken_server), _("Br&oken server"), broken_server)),
			`VSpacing (0.2),
			// text entry label
			// ypbind is the daemon name
			`TextEntry (`id (`options), _("Other &ypbind options"), options),
			`VSpacing (0.2)
			)
		    )
		);

	Wizard::SetContentsButtons (
	    // dialog title
	    _("Expert settings"), contents, help_text,
	    BackButtonLabel (), OKButtonLabel ());

	any result = nil;
	repeat
	{
	    result = UI::UserInput ();
	    if (result == `cancel)
	    {
		result = `abort;
	    }

	    if (result == `next)
	    {
		local_only =  UI::QueryWidget (`id (`local_only), `Value);
		broken_server = UI::QueryWidget (`id (`broken_server), `Value);
		// TODO: disallow " in options
		options = UI::QueryWidget (`id (`options), `Value);

		if (false && some_error)
		{
		    // error popup label
		    UI::MessagePopup (_("The domain name is not correct."));
		    result = nil;
		}
	    }
	}
	until (result == `back || result == `next ||
	       (result == `abort && UI::ReallyAbortPopup (Nis::touched)));

	if (result == `next)
	{
	    Nis::Touch (Nis::local_only != local_only);
	    Nis::local_only = local_only;
	    Nis::Touch (Nis::broken_server != broken_server);
	    Nis::broken_server = broken_server;
	    Nis::Touch (Nis::options != options);
	    Nis::options = options;
	}

	Wizard::RestoreScreenShotName ();
	return result;
    }

    /**
     * The servers dialog
     * @return	`back, `abort or `next
     */
    global define symbol MultipleDialog () ``{
	Wizard::SetScreenShotName ("nis-client-2b-multiple");

	// variable naming: _d means _domain, _s means _server
	string default_d = Nis::GetDomain ();
	boolean global_broadcast = Nis::global_broadcast;
	map all_servers = mapmap (string d, list s, Nis::multidomain_servers,
				  ``( [d, [s, Nis::multidomain_broadcast[d]:false]] ));
	all_servers = add (all_servers, "",
			   [Nis::servers, Nis::default_broadcast]);

	boolean global_broadcast_allowed = global_broadcast;

	// help text
	string help_text = _("<p>Specify the servers for multiple domains and select the default domain.</p>");

	// help text
	help_text = help_text + broadcast_help;

	if (global_broadcast_allowed)
	{
	    // help text
	    // only shown if a deprecated advanced option (%1) is used.
	    help_text = help_text + sformat (_("<p>The global broadcast option (%1) was made obsolete
by the <tt>broadcast</tt> keyword in <tt>/etc/yp.conf</tt>.
This check box only appears if it is turned on.</p>
"),
					     "<tt>ypbind -broadcast</tt>");
	}

	// help text
	help_text = help_text + details_help;

	// help text
	help_text = help_text + _("<p>If you do not need multiple domains, use <b>Single domain</b> to switch to a simpler dialog.</p>");

	// "" means the default domain
	string current_d = "";

	// build the dialog contents
	term multiple =
	    `Frame (
		// frame label
		`id (`multiple), _("Multiple domains"),
		`VBox (
		    `VSpacing (0.2),
		    `Table (
			`id (`domains),
			`opt (`notify, `immediate),
			`header (
			    // table header
			    _("Domain"),
			    // table header
			    `Center (_("Broadcast")),
			    // table header
			    _("Servers")),
			DomainTableItems (default_d, all_servers)
			),
		    `HBox (
			// button label
			`PushButton (`id (`add_d), _("&Add")),
			// button label
			`PushButton (`id (`edit_d), _("E&dit")),
			// button label
			`PushButton (`id (`del_d), _("De&lete"))
			),
		    `VSpacing (0.2)
		    )
		);

	term global_broadcast_t = `Empty ();
	if (global_broadcast_allowed)
	{
	    global_broadcast_t = `CheckBox (
		`id (`global_broadcast), `opt (`notify),
		// check box label
		_("&Global broadcast"), global_broadcast
		);
	}

	term nis_vbox =
	    `VBox (`VSpacing (0.2),
		   `TextEntry (`id (`domain), `opt (`notify),
		   // text entry label
			       _("Default NIS do&main"), default_d),
		   `VSpacing (0.5),
		   `Left (global_broadcast_t),
		   multiple,
		   `VSpacing (0.2),
		   `HBox (
		       // button label
		       `PushButton (`id (`expert), _("D&etails ...")),
		       // button label
		       `PushButton (`id (`single), _("&Single domain <<"))
		       ),
		   `VSpacing (0.2)
		);

	term nis_frame =
	    // frame label
	    `Frame (_("NIS client"), nis_vbox);

	term contents = nis_vbox;

	Wizard::SetContentsButtons (
	    sformat ("%1 - %2",
		     // dialog title
		     _("Configuration of NIS client"),
		     // dialog subtitle
		     _("Multiple domains")), contents, help_text,
	    BackButtonLabel (), FinishButtonLabel ());

	//UI::ChangeWidget (`id (`domains), `CurrentItem, current_d);

	any result = nil;
	// preselect an item
	// there is always the first item - the default domain
	UI::ChangeWidget (`id (`domains), `CurrentItem, "");

	while (true)
	{
	    current_d = UI::QueryWidget (`id (`domains), `CurrentItem);
	    boolean any_d = current_d != nil;

	    UI::ChangeWidget (`id (`edit_d), `Enabled, any_d);
	    // deleting the defalut domain
	    // actually deletes only the server list
	    UI::ChangeWidget (`id (`del_d), `Enabled, any_d
			      /*&& current_d !=""*/);

	    UI::ChangeWidget (`id (`multiple), `Enabled, !global_broadcast);

	    // Kludge, because a `Table still does not have a shortcut.
	    // exclude textentry-notify
	    if (result != `domain)
	    {
		UI::SetFocus (`id (`domains));
	    }

	    result = UI::UserInput ();
	    if (result == `cancel)
	    {
		result = `abort;
	    }

	    // only query if the widget is there
	    global_broadcast = global_broadcast_allowed &&
		UI::QueryWidget (`id (`global_broadcast), `Value);

	    if (result == `domain)
	    {
		default_d = UI::QueryWidget (`id (`domain), `Value);
		// update the Default: item
		UI::ChangeWidget (`id (`domains), `Item ("", 0),
				  DefaultCell (default_d));
	    }

	    // switch
	    if (result == `add_d)
	    {
		list name_servers = DomainPopup (nil, default_d, [[], false], mapkeys (all_servers));
		if (name_servers != nil)
		{
		    string d = name_servers[0]:nil;
		    list s_sp = name_servers[1]:[];
		    all_servers = add (all_servers, d, s_sp);
		    // show these items, d selected
		    UpdateDomainTable (default_d, all_servers, d);
		}
	    }
	    else if (result == `edit_d)
	    {
		string d0 = UI::QueryWidget (`id (`domains), `CurrentItem);

		if (d0 != nil)
		{
		    // editing the default domain is a special case
		    list name_servers = DomainPopup (d0, default_d,
						     all_servers[d0]:[],
						     mapkeys (all_servers));
		    if (name_servers != nil)
		    {
			string d = name_servers[0]:nil;
			list s_sp = name_servers[1]:[];
			string newkey = (d0=="")? "": d;
			all_servers = mapmap (string k, list v, all_servers,``(
						  (k == d0) ?
						  [ newkey, s_sp ] :
						  [ k, v]
						  ));
			// update the default domain box
			if (d0 == "")
			{
			    default_d = d;
			    UI::ChangeWidget (`id (`domain),`Value, default_d);
			}
			// show these items, d selected
			// TODO: if it flickers,
			//  only replace the old line by a new one
			UpdateDomainTable (default_d, all_servers, newkey);
		    }
		}
	    }
	    else if (result == `del_d)
	    {
		string d0 = UI::QueryWidget (`id (`domains), `CurrentItem);
		if (d0 == "")
		{
		    Wizard::SetScreenShotName ("nis-client-2b-del-dfl");
		    // do not delete the default domain, just the servers
		    // Translators: a yes-no popup
		    if (UI::YesNoPopup (_("Really delete the server list
for the default domain?")))
		    {
			all_servers = add (all_servers, "", [[], false]);
		    }
		    Wizard::RestoreScreenShotName ();
		    // show these items, the default domain selected
		    UpdateDomainTable (default_d, all_servers, "");
		}
		else if (d0 != nil)
		{
		    Wizard::SetScreenShotName ("nis-client-2b-del-dom");
		    // Translators: a yes-no popup
		    if (UI::YesNoPopup (_("Really delete this domain?")))
		    {
			all_servers = filter (string k, list v,
					      all_servers, ``(k != d0));
		    }
		    Wizard::RestoreScreenShotName ();
		    // show these items, the default domain selected
		    UpdateDomainTable (default_d, all_servers, "");
		}
	    }
	    else if (contains ([`next, `expert, `single], result))
	    {
		// Input validation
		default_d = UI::QueryWidget (`id (`domain), `Value);

		if (!Nis::check_nisdomainname (default_d))
		{
		    UI::SetFocus (`id (`domain));
		    Wizard::SetScreenShotName ("nis-client-2b1-err");
		    UI::ErrorPopup (Nis::valid_nisdomainname ());
		    Wizard::RestoreScreenShotName ();
		}
		else
		{
		    // all checks OK, break the input loop
		    // Warn about discarding when `single
		    //  or warn before checking?
		    if (result != `single ||
			size (all_servers) <= 1 ||
			UI::ContinueCancelPopup(
// Translators: Continue/Cancel popup
_("The settings for all domains except
the default domain will be lost.")))
		    {
			break;
		    }
		}
	    }
	    else if (result == `back || (result == `abort &&
					 UI::ReallyAbortPopup (Nis::touched)))
	    {
		break;
	    }
	}

	if (contains ([`next, `expert, `single], result))
	{
	    Nis::SetDomain (default_d);
	    Nis::Touch (Nis::global_broadcast != global_broadcast);
	    Nis::global_broadcast = global_broadcast;

	    map only_servers = mapmap (string d, list v, all_servers, ``(
					   [d, v[0]:[]]
					   ));
	    list servers = only_servers[""]:[];
	    boolean default_broadcast = nil;
	    map multidomain_servers = nil;
	    map multidomain_broadcast = nil;
	    if (result == `single)
	    {
		//TODO default_broadcast
		default_broadcast = false;
		multidomain_servers = $[];
		multidomain_broadcast = $[];
	    }
	    else
	    {
		map only_broadcast = mapmap (string d, list v, all_servers, ``(
						 [d, v[1]:false]
						 ));
		default_broadcast = only_broadcast[""]:false;
		multidomain_servers = filter (string d, list v,
					      only_servers, ``( d != "") );
		multidomain_broadcast = filter (string d, boolean v,
						only_broadcast, ``( d != "") );
	    }
	    Nis::Touch (Nis::servers != servers);
	    Nis::servers = servers;
	    Nis::Touch (Nis::default_broadcast != default_broadcast);
	    Nis::default_broadcast = default_broadcast;
	    Nis::Touch (false); //TODO need to know earlier for abort?
	    Nis::multidomain_servers = multidomain_servers;
	    Nis::Touch (false); //TODO need to know earlier for abort?
	    Nis::multidomain_broadcast = multidomain_broadcast;
	}

	Wizard::RestoreScreenShotName ();
	return result;
    }

    /**
     * @param	m	a map
     * @return		keys of the map
     */
    global define list mapkeys (map m) ``{
	return maplist (any k, any v, m, ``( k ));
    }

    /**
     * @tuple server_sp
     * 0 list(string)	server list
     * 1 boolean	broadcast
     */

    /**
     * Add/Edit a domain, including its name and servers
     * @param	init		currently selected domain: nil=add, ""=default
     * @param	default_d	the default domain
     * @param	server_sp	@ref server_sp
     * @param	existing	existing domains
     * @return [name, [ [server1, server2], broadcast? ]]
     */
    global define list DomainPopup (string init, string default_d,
				    list server_sp, list(string) existing) ``{
	Wizard::SetScreenShotName ("nis-client-2b1-domain");

	string domain = (init == nil)? "": (init == "")? default_d: init;
	list servers = server_sp[0]:[];
	boolean broadcast = server_sp[1]:false;

	term t_servers =
	    `VBox (
		`ReplacePoint (`id (`r_servers), ServerBox (servers)),
		`CheckBox (`id (`local_broadcast),
			   // checkbox label
			   _("&Broadcast"), broadcast),
		`HBox (
		    // button label
		    `PushButton (`id (`add_s), _("&Add")),
		    // button label
		    `PushButton (`id (`edit_s), _("&Edit")),
		    // button label
		    //&Delete could be activated if the user wanted &Domain
		    `PushButton (`id (`del_s), _("De&lete"))
		    )
		);

	term contents =
	    `HBox(
		`HSpacing(1),
		`VBox(
		    `VSpacing(0.2),
		    // Translators: popup dialog heading
//		    `Heading (_("Domain name")),
		    // Add a domain, Adding a domain? Edit...
		    // Translators: text entry label
		    `Left (`TextEntry (`id (`domain), _("&Domain name"), domain)),
		    `VSpacing(0.5),
		    t_servers,
		    `VSpacing(0.2),
		    `HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
			  `PushButton(`id(`cancel), CancelButtonLabel())),
		    `VSpacing(0.2)
		    ),
		`HSpacing(1)
		);

	UI::OpenDialog (`opt(`decorated), contents);
	UI::SetFocus (`id (`domain));

	any ui = nil;
	while (true)
	{
	    boolean any_s = UI::QueryWidget (`id (`servers), `CurrentItem) != nil;
	    UI::ChangeWidget (`id (`edit_s), `Enabled, any_s);
	    UI::ChangeWidget (`id (`del_s), `Enabled, any_s);

	    ui = UI::UserInput ();
	    if (ui == `cancel)
	    {
		break;
	    }
	    else if (ui == `ok)
	    {
		// Input validation
		// all querywidgets done now for consistency
		domain = UI::QueryWidget (`id (`domain), `Value);
		// servers are kept up to date
		broadcast = UI::QueryWidget (`id (`local_broadcast), `Value);

		if (!Nis::check_nisdomainname (domain)) //also disallows ""
		{
		    UI::SetFocus (`id (`domain));
		    UI::ErrorPopup (Nis::valid_nisdomainname ());
		}
		else if (init != "" && domain != init &&
			 contains (existing, domain))
		{
		    UI::SetFocus (`id (`domain));
		    // Translators: error message
		    UI::ErrorPopup (_("This domain is already defined."));
		}
		else
		{
		    // all checks OK, break the input loop
		    break;
		}
	    }
	    else if (ui == `add_s)
	    {
		string s = InputServer ("", servers);
		if (s != nil)
		{
		    servers = add (servers, s);
		    ServerBoxSet (servers);
		    UI::ChangeWidget (`id (`servers), `CurrentItem, s);
		}
	    }
	    else if (ui == `edit_s)
	    {
		string s0 = UI::QueryWidget (`id (`servers), `CurrentItem);
		if (s0 != nil)
		{
		    string s = InputServer (s0, servers);
		    if (s != nil)
		    {
			servers = maplist (string e, servers, ``(
					       (e == s0)? s: e
					       ));
			ServerBoxSet (servers);
			UI::ChangeWidget (`id (`servers), `CurrentItem, s);
		    }
		}
	    }
	    else if (ui == `del_s)
	    {
		string s0 = UI::QueryWidget (`id (`servers), `CurrentItem);
		if (s0 != nil)
		{
		    // probably no confirmation.
		    servers = filter (string e, servers, ``( e != s0 ));
		    ServerBoxSet (servers);
		    if (size (servers) > 0)
		    {
			s = servers[0]:"";
			UI::ChangeWidget (`id (`servers), `CurrentItem, s);
		    }
		}
	    }
	}

	UI::CloseDialog ();
	Wizard::RestoreScreenShotName ();
	return (ui == `ok)? [domain, [servers, broadcast]] : nil;
    }

    /**
     * @param default_d the default domain
     * @return "Default: default_d"
     */
    global define string DefaultCell (string default_d) ``{
	// Translators: the default NIS domain
	return sformat (_("Default: %1"), default_d);
    }

    global string check_g = nil;
    /**
     * Constructs items for the domain table
     * @param	default_d	the default domain
     * @param	all_servers	map of @ref server_sp
     * @return a list of items
     * @see Nis#multidomain_servers TODO
     */
    global define list DomainTableItems (string default_d, map all_servers) ``{
	if (check_g == nil)
	{
	    check_g = UI::Glyph (`CheckMark);
	}
	// maps are sorted, so the default domain, "", comes first
	return maplist (string d, list server_sp, all_servers, ``(
			    `item (
				`id (d),
				(d == "")? DefaultCell (default_d): d,
				// this would be a priority example x ]:
				(server_sp[1]:false)? check_g: "",
				mergestring (server_sp[0]:[], ", ")
				)
			    ));
    }

    /**
     * @param	default_d	the default domain
     * @param	all_servers show these items
     * @param	d	the selected item
     */
    global define void UpdateDomainTable (string default_d, map all_servers,
					  string d) ``{
	UI::ChangeWidget (`id (`domains), `Items,
			  DomainTableItems (default_d, all_servers));
	UI::ChangeWidget (`id (`domains), `CurrentItem, d);
    }

    /**
     * Used for adding and editing a server
     * @param init	initial value
     * @param existing	current server list (excluding default?)
     * @return a server or nil
     */
    global define string InputServer (string init, list(string) existing) ``{
	Wizard::SetScreenShotName ("nis-client-2b11-server");
	string server = init;

	term contents =
	    `HBox(
		`HSpacing(1),
		`VBox(
		    `VSpacing(0.2),
		    // Translators: popup dialog heading
//		    `Heading (_("Server address")),
		    // Translators: text entry label
		    `Left (`TextEntry (`id (`server), _("&Server address"), server)),
		    `VSpacing(0.2),
		    `HBox(`PushButton(`id(`ok), `opt(`default), OKButtonLabel()),
			  `PushButton(`id(`cancel), CancelButtonLabel())),
		    `VSpacing(0.2)
		    ),
		`HSpacing(1)
		);

	UI::OpenDialog (`opt(`decorated), contents);
	UI::SetFocus (`id (`server));

	any ret = nil;
	while (true)
	{
	    ret = UI::UserInput ();
	    if (ret == `cancel)
	    {
		break;
	    }
	    else if (ret == `ok)
	    {
		// Input validation
		server = UI::QueryWidget (`id (`server), `Value);

		if (!Nis::check_address_nis (server))
		{
		    UI::SetFocus (`id (`server));
		    UI::ErrorPopup (Nis::valid_address_nis ());
		}
		else if (contains (existing, server) && server != init)
		{
		    UI::SetFocus (`id (`server));
		    // Translators: error message
		    UI::ErrorPopup (_("This server is already specified."));
		}
		else
		{
		    // all checks OK, break the input loop
		    break;
		}
	    }
	}

	UI::CloseDialog ();
	Wizard::RestoreScreenShotName ();
	return (ret == `ok)? server : nil;
    }

    /**
     * Updates the server box when a domain has changed
     * @param	servers	a server list
     */
    global define void ServerBoxSet (list(string) servers) ``{
	UI::ReplaceWidget (`id (`r_servers), ServerBox (servers));
    }

    /**
     * Constructs a whole SelectionBox
     * because we cannot replace its items (#8826)
     * @param	servers	a server list
     * @return a SelectionBox
     */
    global define term ServerBox (list(string) servers) ``{
	return `SelectionBox (
	    `id (`servers), `opt (`notify/*, `shrinkable*/),
	    _("&Servers"),
	    servers);
    }


    /**
     * Confirmation dialog
     * @return `back or `next
     */
    global define symbol SaveDialog () ``{
	Wizard::SetScreenShotName ("nis-client-3-save");
	// continue-cancel popup
	string message1 = _("The configuration of the NIS client will be saved.\n");
	string message2 = Nis::ProbePackages ();
	boolean ret = UI::ContinueCancelPopup (message1 + message2);

	Wizard::RestoreScreenShotName ();
	return ret ? `next : `back;
    }
}
