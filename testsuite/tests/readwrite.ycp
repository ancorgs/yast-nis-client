/**
 * Module:
 *   NIS client configuration
 *
 * Summary:
 *   Testsuite
 *
 * Authors:
 *   Martin Vidner <mvidner@suse.cz>
 *
 * $Id$
 */
{
    // testedfiles: Nis.ycp Autologin.ycp Service.ycp Report.ycp Testsuite.ycp Nsswitch.ycp

    include "testsuite.ycp";

    map READ_INIT = $[
	"target" : $[
	    "size"	: 0,
	],
    ];
    map EXEC_INIT = $[
	"target" : $[
	    "bash_output" : $[
	    ],
	],
    ];

    TESTSUITE_INIT ([READ_INIT, $[], EXEC_INIT], nil);

    import "Pkg"; // override
    import "Nis";


    map dhcp_yes = $[
	"DHCLIENT_MODIFY_NIS_CONF": "yes",
	"DHCLIENT_SET_DOMAINNAME": "yes",
	];
    map dhcp_no = $[
	"DHCLIENT_MODIFY_NIS_CONF": "no",
	"DHCLIENT_SET_DOMAINNAME": "no",
	];

    map READ = $[
	// Runlevel:
	"init": $[
	    "scripts": $[
		"exists": true,
		"runlevel": $[
		    "portmap": $[
			"start": [ "3", "5"],
			"stop":	 [ "3", "5"],
			],
		    "ypbind": $[
			"start": [ "3", "5"],
			"stop":	 [ "3", "5"],
			],
		    "autofs": $[
			"start": [ "3", "5"],
			"stop":	 [ "3", "5"],
			],
		    ],
		// their contents is not important for ServiceAdjust
		"comment": $[
		    "portmap": $[],
		    "ypbind": $[],
		    "autofs": $[],
		    ],
		],
	    ],
	// Nis itself:
	"etc": $[
	    "yp_conf": $[
		"servers": [ "10.20.30.40", "10.20.30.80" ],
		"slp":	$[ "slpdomain" : true ],
		"defaultbroadcast": false,
		"domainservers": $[
		    "otherdomain": [ "1.2.3.4" ],
		    ],
		"broadcast": $[
		    "otherdomain": true,
		    ],
		],
//	    "defaultdomain": "mydomain",
	    "nsswitch_conf": $[
		"passwd": "compat",
		"group": "compat",
		"hosts": "files dns6",
		"automount": "files",
		"services"	: "files",
		"netgroup"	: "files",
		"aliases"	: "files",
		],
	    ],
	"sysconfig": $[
	    "ypbind": $[
		"YPBIND_LOCAL_ONLY": "no",
		"YPBIND_BROADCAST": "no",
		"YPBIND_BROKEN_SERVER": "no",
		"YPBIND_OPTIONS": "",
		],
	    "network": $[
		"dhcp": dhcp_no,
		],
	    "displaymanager": $[
		"DISPLAYMANAGER": "kdm",
		"DISPLAYMANAGER_AUTOLOGIN": "no",
		"DISPLAYMANAGER_PASSWORD_LESS_LOGIN": "no",
		],
	],
	"target"	: $[
	    "size"	: 0,
	],
    ];

    map WRITE = $[
	];

    map EXECUTE = $[
	"target": $[
	    // ok if used both for `domainname` and `rcypbind start`
	    "bash_output": $[
		"exit": 0,
		"stdout": "mydomain\n",
		"stderr": "",
		],
	    "remove": true, // /etc/yp.conf.sv
	    ],
	];

    DUMP ("DHCP off");
    TEST (``(Nis::Read ()), [READ, WRITE, EXECUTE], nil);
    TEST (``(Nis::Write ()), [READ, WRITE, EXECUTE], nil);

    DUMP ("DHCP on");
    READ["sysconfig", "network", "dhcp"] = dhcp_yes;
    TEST (``(Nis::Read ()), [READ, WRITE, EXECUTE], nil);
    TEST (``(Nis::Touch (true)), [], nil);
    TEST (``(Nis::Write ()), [READ, WRITE, EXECUTE], nil);
}
